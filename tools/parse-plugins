#!/usr/bin/env perl
use 5.16.0;
use warnings;

use JSON qw( encode_json );
use Method::Signatures;
use XML::Simple qw( XMLin );
use YAML::Tiny qw( );

exit main(@ARGV);

func load_xml($filename) {
    my $xml = XMLin($filename, ForceArray => [qw( Url )] );

    my $name = $xml->{ShortName};
    my ($url) = grep { $_->{type} eq 'text/html' } @{ $xml->{Url} };

    die "No text/html url found in $filename"
        unless $url;

    die "Method \"$url->{method}\" not yet supported in $filename"
        unless $url->{method} eq 'get';

    return {
        name => $name,
        url  => $url->{template},
    };
}

func dump_tt2 ($plugins) {
    say '[%- SET plugins = [';
    for my $plugin (@$plugins) {
        say qq(\t{ name => "$plugin->{name}", url => "$plugin->{url}" },);
    }
    say '] -%]';
}

func dump_json($plugins) {
    print encode_json($plugins);
}

func dump_yaml($plugins) {
    print YAML::Tiny::Dump($plugins);
}

func main(@filenames) {
    my @plugins = map { load_xml($_) } @filenames;
    #dump_json(\@plugins);
    #dump_tt2(\@plugins);
    dump_yaml(\@plugins);
    return 0;
}

